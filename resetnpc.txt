//===== rAthena Script =======================================
//= Reset NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//============================================================
prontera,153,51,6	script	重置人員	811,{
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	5000,	5000,	9000,	0;
	mes "[ 重置人員 ]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "抱歉，您一生只能夠重置"+callfunc("F_InsertPlural",.@Reset[3],"次")+"!";
		close;
	}
	mes "我是重置人員";
	mes "重置角色點數: "+ callfunc("F_InsertComma",.@Reset[1]) +"元";
	mes "重置技能點數: "+ callfunc("F_InsertComma",.@Reset[0]) +"元";
	mes "重置角色、技能點數: "+ callfunc("F_InsertComma",.@Reset[2]) +"元";
	if(.@Reset[3]) mes "^FF3355您可能只能重置"+callfunc("F_InsertPlural",.@Reset[3],"次")+"，請審慎使用"+((.@Reset[3]>1)?"它們":"它")+"!^000000";
	mes "請選擇您要的服務:";
	next;
	set .@i,(select("^FF3355重置技能點數:重置角色點數:重置角色、技能點數^000000:取消"));
	if(.@i > 3) close;
	mes "[ 重置人員 ]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "抱歉，您沒有足夠的錢!";
		close;
	}
	if(.@Reset[3]){
		mes "這一生只能重置"+callfunc("F_InsertPlural",.@Reset[3],"次")+"，您確定要重置嗎?";
		if(select("讓我再想一想:沒問題") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end SC_ALL;// TODO make a sc_end current classes sc only
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "您可以離開了!";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
}
